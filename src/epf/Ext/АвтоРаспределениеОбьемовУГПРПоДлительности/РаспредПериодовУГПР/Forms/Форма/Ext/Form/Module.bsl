
&НаКлиенте
Процедура ВыполнитьРаспределение(Команда)
	Если УГПР.Пустая()  Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не выбран УГПР!");
		возврат;
 	КонецЕсли;
	
	ВыполнитьРаспределениеНаСервере();

	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обработка завершена!");
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьРаспределениеНаСервере()
	
	ДокументПериоды = Документы.ИНАГРО_УГПР.ДокументСПериодамиСсылка(УГПР);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧПериоды.КлючСтроки,
		|	СУММА(ТЧПериоды.Длительность) КАК Длительность
		|ПОМЕСТИТЬ ВТ_Отбор
		|ИЗ
		|	Документ.ИНАГРО_ПериодыРаботСлужебный.РаботыПериод КАК ТЧПериоды
		|ГДЕ
		|	ТЧПериоды.Ссылка = &СсылкаПериоды
		|
		|СГРУППИРОВАТЬ ПО
		|	ТЧПериоды.КлючСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЧРаботы.КлючСтроки,
		|	ТЧРаботы.Объем,
		|	ВТ_Отбор.Длительность КАК БазаДлительность
 		|ИЗ
		|	Документ.ИНАГРО_УГПР.Работы КАК ТЧРаботы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Отбор КАК ВТ_Отбор
		|		ПО ТЧРаботы.КлючСтроки = ВТ_Отбор.КлючСтроки
		|ГДЕ
		|	ТЧРаботы.Ссылка = &СсылкаУГПР";
	
	Запрос.УстановитьПараметр("СсылкаПериоды", ДокументПериоды);
	Запрос.УстановитьПараметр("СсылкаУГПР", УГПР);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если не РезультатЗапроса.Пустой() Тогда
		
		ДокументПериодыОбъект = ДокументПериоды.ПолучитьОбъект();
 		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Для каждого СтрокаПериод Из ДокументПериодыОбъект.РаботыПериод Цикл
			
			ВыборкаДетальныеЗаписи.Сбросить();
			
			Если ВыборкаДетальныеЗаписи.НайтиСледующий(Новый Структура("КлючСтроки", СтрокаПериод.КлючСтроки)) Тогда
				
				СтрокаПериод.Объем =  (ВыборкаДетальныеЗаписи.Объем /ВыборкаДетальныеЗаписи.БазаДлительность) * СтрокаПериод.Длительность;	
				
			КонецЕсли;
			
		КонецЦикла;

		Попытка
			
			ДокументПериодыОбъект.Записать(РежимЗаписиДокумента.Запись);
			
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка записи документа периоды работ!"+ОписаниеОшибки());
			
		КонецПопытки;

	КонецЕсли;  //Если не РезультатЗапроса.Пустой() Тогда

КонецПроцедуры
