#Область Сервер
&НаСервере
Перем КэшНормативныхСборников;
Перем КэшРесурсов;
Перем КэшРаботПроекта;



//ОСНОВА АЛГОРИТМА
&НаСервере
Процедура ЧтениеФайла(ВнутреннийАдресСервера)
	
	//получаем файл из "временного хранилища сервера" и сохраняем во временный файл на сервере
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ПолучитьИзВременногоХранилища(ВнутреннийАдресСервера).Записать(ИмяВременногоФайла);
	
	Чтение = Новый ЧтениеXML;
	Чтение.ОткрытьФайл(ИмяВременногоФайла);
	
	СметаХДТО = ФабрикаXDTO.ПрочитатьXML(Чтение);
	
	
	Попытка		
		СтуктураХДТО = СметаХДТО.Получить("СТРУКТУРА");
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Неверный формат файла, "+ ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Если  СметаХДТО.Свойства().Получить("ПоказательСП") <> Неопределено  Тогда
		ПоказательСП =   СтрокаВЧисло(СметаХДТО.ПоказательСП);
	КонецЕсли;
	
	Если  СметаХДТО.Свойства().Получить("ПоказательНК2") <> Неопределено  Тогда
		ПоказательНК2 =  СтрокаВЧисло(СметаХДТО.ПоказательНК2);
	КонецЕсли;

	Если  СметаХДТО.Свойства().Получить("Составил") <> Неопределено  Тогда
		Составил = СметаХДТО.Составил;
	КонецЕсли;
	
	Если  СметаХДТО.Свойства().Получить("ШифрСметы") <> Неопределено  Тогда
		ШифрСметы = СметаХДТО.ШифрСметы;
	КонецЕсли;

	
	Если  СметаХДТО.Свойства().Получить("ВерсияГенератора") <> Неопределено  Тогда
		ВерсияГенератора = СокрЛП(СметаХДТО.ВерсияГенератора);
 		ИзменитьВерсиюСметы();
	КонецЕсли;
	
	
	Если АвтозаполнениеНормативныхСборников Тогда
		
		КэшНормативныхСборников = ЗаполнитьКэшНормативныхСборников();
		
	КонецЕсли;
	
	
	Если Не БезРесурсов Тогда
		КэшРесурсов = ЗаполнитьКэшРесурсов();
	КонецЕсли;
	
	КэшРаботПроекта = ЗаполнитьКэшРабот(Объект.Проект);
	
 	
	Если СметаХДТО.Свойства().Получить("ИТОГДОК") <> Неопределено Тогда 

		  ИтогоПоСмете = ИтогоПоСмете + СметаХДТО.ИТОГДОК.ВСЕГО;
		
	КонецЕсли;
	
	// Очистка
	ТекущийЭлементДерева = РаботыДЗ;
	
	
	// КОРЕНЬ 
 	НЭ = ТекущийЭлементДерева.ПолучитьЭлементы().Добавить();
	НЭ.Название = СметаХДТО.НаименованиеДок;
	НЭ.КлючСтроки = Новый УникальныйИдентификатор;
	НЭ.КлючСвязи = "00000000-0000-0000-0000-000000000000";
	
	ЗаполнитьЗначенияСвойств(НЭ, СметаХДТО);
	
	НЭ.ВидРаботы = НайтиКонструктив(НЭ, Объект.Проект, АвтосозданиеWBS1c);
	НЭ.КодWBS = ШифрСметы;
	
	
	ТекущийЭлементДерева = НЭ;
	
	
	Если СтуктураХДТО <> Неопределено Тогда
 		
		Если СтуктураХДТО.Свойства().Получить("РАЗДЕЛ") <> Неопределено Тогда // структура как разделы
 			
			Если ТипЗнч(СтуктураХДТО.РАЗДЕЛ) = Тип("СписокXDTO") Тогда
				РазделыХДТО = СтуктураХДТО.ПолучитьСписок("РАЗДЕЛ");
			иначе
				РазделыХДТО = Новый Массив;
				РазделыХДТО.Добавить(СтуктураХДТО.Получить("РАЗДЕЛ"));
 			КонецЕсли;
   			
			Если РазделыХДТО <> Неопределено Тогда
				
				РазобратьСписокХДТО(ТекущийЭлементДерева, РазделыХДТО);
				
			КонецЕсли;
			
		КонецЕсли;
		
		
		Если СтуктураХДТО.Свойства().Получить("ПОЗИЦИЯ") <> Неопределено Тогда // структура как позиции
			
 			Если ТипЗнч(СтуктураХДТО.ПОЗИЦИЯ) = Тип("СписокXDTO") Тогда
				РазделыХДТО = СтуктураХДТО.ПолучитьСписок("ПОЗИЦИЯ");
			иначе
				РазделыХДТО = Новый Массив;
				РазделыХДТО.Добавить(СтуктураХДТО.Получить("ПОЗИЦИЯ"));
 			КонецЕсли;
			
 			Если РазделыХДТО <> Неопределено Тогда
				
				РазобратьСписокХДТО(ТекущийЭлементДерева, РазделыХДТО);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	 
	 
КонецПроцедуры
 
&НаСервере
Процедура РазобратьСписокХДТО(ТекущийЭлементДерева, РазделыХДТО)
 	
	Для каждого ЭлементСпискаХДТО Из РазделыХДТО Цикл
		
		
		
		НЭ = ДобавитьЭлементВДерево(ТекущийЭлементДерева, ЭлементСпискаХДТО, (ЭлементСпискаХДТО.ВладеющееСвойство().Имя = "ПОЗИЦИЯ"), (ЭлементСпискаХДТО.ВладеющееСвойство().Имя = "РЕСУРС"));
		
 		// разобрать подчиненные
		
 		// РАЗДЕЛЫ
 		Если ЭлементСпискаХДТО.Свойства().Получить("РАЗДЕЛ") <> Неопределено Тогда 
			
			Если ТипЗнч(ЭлементСпискаХДТО.РАЗДЕЛ) = Тип("СписокXDTO") Тогда
				ПозицииХДТО = ЭлементСпискаХДТО.ПолучитьСписок("РАЗДЕЛ");
			иначе
				ПозицииХДТО = Новый Массив;
				ПозицииХДТО.Добавить(ЭлементСпискаХДТО.Получить("РАЗДЕЛ"));
 			КонецЕсли;
			
			РазобратьСписокХДТО(НЭ, ПозицииХДТО); //рекурсия
			
		КонецЕсли; //Если ЭлементСпискаХДТО.Свойства().Получить("РАЗДЕЛ")	
		
		// ПОЗИЦИИ
		Если ЭлементСпискаХДТО.Свойства().Получить("ПОЗИЦИЯ") <> Неопределено Тогда			
			
 			
			Если ТипЗнч(ЭлементСпискаХДТО.ПОЗИЦИЯ) = Тип("СписокXDTO") Тогда
				ПозицииХДТО = ЭлементСпискаХДТО.ПолучитьСписок("ПОЗИЦИЯ");
				РазобратьСписокХДТО(НЭ, ПозицииХДТО);
			иначе
				
				ПозицииХДТО = Новый Массив;
				ПозицииХДТО.Добавить(ЭлементСпискаХДТО.Получить("ПОЗИЦИЯ"));

				//ПозицииХДТО = ЭлементСпискаХДТО.Получить("ПОЗИЦИЯ");
				//ДобавитьЭлементВДерево(НЭ, ПозицииХДТО, Истина);
				РазобратьСписокХДТО(НЭ, ПозицииХДТО);
			КонецЕсли;
 			
 			
		КонецЕсли;  //Если ЭлементСпискаХДТО.Свойства().Получить("ПОЗИЦИЯ") 
 		
		//РЕСУРСЫ
		Если ЭлементСпискаХДТО.Свойства().Получить("РЕСУРС") <> Неопределено и не БезРесурсов Тогда 
			
 			Если ТипЗнч(ЭлементСпискаХДТО.РЕСУРС) = Тип("СписокXDTO") Тогда
				ПозицииХДТО = ЭлементСпискаХДТО.ПолучитьСписок("РЕСУРС");
				РазобратьСписокХДТО(НЭ, ПозицииХДТО);
			иначе
				ПозицииХДТО = ЭлементСпискаХДТО.Получить("РЕСУРС");
				ДобавитьЭлементВДерево(НЭ, ПозицииХДТО, , Истина);
			КонецЕсли;
			
			Если флФорматСметы=1 Тогда
				СкорректироватьОплатуМашинистов(НЭ);
			КонецЕсли;
			
 
		ИначеЕсли НЭ.Тип = "0" и НЭ.Сумма <> 0 Тогда // это значится как работа, есть стоимость но нет ресурсов ?!
		//	/// до выяснения причин таких записей создаем фиктивный ресурс материал
		//	
			ОбщегоНазначенияклиентСервер.СообщитьПользователю(" " + ЭлементСпискаХДТО.Наименование + " (Работа без ресурсов)");
		//	ДобавитьЭлементВДерево(НЭ, ЭлементСпискаХДТО, Ложь, Истина);

		КонецЕсли;	//Если ЭлементСпискаХДТО.Свойства().Получить("РЕСУРС")
 		
	КонецЦикла;
 	
КонецПроцедуры

&НаСервере
Функция  ДобавитьЭлементВДерево(ТекущийЭлементДерева, ЭлементСпискаХДТО, ЭтоРабота = Ложь, ЭтоРесурс = Ложь)
	
	
	Если ЭтоРабота и ЭлементСпискаХДТО.Тип = "3" Тогда // пока непонятна роль этих ресурсов
		
		Возврат ТекущийЭлементДерева;
		
	КонецЕсли;
	
	// Общие реквизиты
	НовСтр = ТекущийЭлементДерева.ПолучитьЭлементы().Добавить();
	НовСтр.Название = ЭлементСпискаХДТО.Наименование;
	НовСтр.КлючСтроки = Новый УникальныйИдентификатор;
	НовСтр.КлючСвязи = ТекущийЭлементДерева.КлючСтроки;
	
	ЗаполнитьЗначенияСвойств(НовСтр, ЭлементСпискаХДТО);
	
	 //НовСтр.ЭтоРесурс  = (ЭлементСпискаХДТО.ВладеющееСвойство().Имя = "РЕСУРС"); выненесено в общий обход дерева
	 //НовСтр.ЭтоРабота  = (ЭлементСпискаХДТО.ВладеющееСвойство().Имя = "ПОЗИЦИЯ");
	 
	 НовСтр.ЭтоРесурс  = ЭтоРесурс;
	 НовСтр.ЭтоРабота  = ЭтоРабота;

	
	
	Если ЭлементСпискаХДТО.Свойства().Получить("Объем") <> Неопределено Тогда
			
		НовСтр.Объем = СтрокаВЧисло(ЭлементСпискаХДТО.Объем);
			
	КонецЕсли;
	
	Если ЭтоРабота и ЭлементСпискаХДТО.Свойства().Получить("ID") <> Неопределено Тогда
			
		НовСтр.КодWBS = ЭлементСпискаХДТО.ID;
			
	КонецЕсли;
		

	// постобработка
	Если НовСтр.ЭтоРабота Тогда   // работа
		
		НовСтр.НормативныйСборник = ЗаполнитьНормативныйСборник(НовСтр.КодСНБ);
		
		НовСтр.ЕдиницаИзмерения = НайтиСоздатьединицуИзмерения_смета(НовСтр.Измеритель);
		
 		НовСтр.ВидРаботы = НайтиРаботу(НовСтр, Объект.Проект, КэшРаботПроекта, АвтосозданиеWBS1c);
 			
		ЗаполнитьСтоимости(ЭлементСпискаХДТО, НовСтр, ПоказательСП, флФорматСметы);
		
		Если НовСтр.Тип = "6" или  НовСтр.Тип = "1" или  НовСтр.Тип = "2" Тогда // Работа-ресурс
			
			Если не БезРесурсов Тогда
				ДобавитьЭлементВДерево(НовСтр, ЭлементСпискаХДТО, Ложь, Истина);
			КонецЕсли;
		ИначеЕсли  НовСтр.Тип = "0" Тогда// обычная работа
			
		Иначе	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Тип работы не обрабатывается, обратитесь к разработчику: тип "+НовСтр.Тип);
		КонецЕсли;
		
 		
	ИначеЕсли не НовСтр.ЭтоРабота и не НовСтр.ЭтоРесурс Тогда   // конструктив		
		
		НовСтр.ВидРаботы = НайтиКонструктив(НовСтр, Объект.Проект, АвтосозданиеWBS1c);
		
 		
	ИначеЕсли  НовСтр.ЭтоРесурс и не БезРесурсов Тогда   // ресурс	
		
		ДобавитьЭлементВДерево_Ресурс(ТекущийЭлементДерева, ЭлементСпискаХДТО, НовСтр);
		
	КонецЕсли;
	
	Возврат НовСтр;
	
КонецФункции



&НаСервере
Функция ДобавитьЭлементВДерево_Ресурс(ТекущийЭлементДерева, ЭлементСпискаХДТО, НовСтр)
	
	НовСтр.КлючСтроки = ТекущийЭлементДерева.КлючСтроки;
	
	НовСтр.ЕдиницаИзмерения = НайтиСоздатьединицуИзмерения_смета(НовСтр.Измеритель);
	
	
	// 1 РАСЧЕТ КОЛИЧЕСТВА 
	
	Если ЭлементСпискаХДТО.Свойства().Получить("Норма") <> Неопределено Тогда
		
		Если флФорматСметы = 0 или флФорматСметы = 2 Тогда   // норма как норматив на единицу работы
			
			НовСтр.НормаРасхода = СтрокаВЧисло(ЭлементСпискаХДТО.Норма);
			НовСтр.Объем =  Окр(НовСтр.НормаРасхода  *  ТекущийЭлементДерева.Объем, 3);
			
			Если  флФорматСметы = 2 или НовСтр.Объем = 0 ИЛИ НовСтр.Тип = "2"  Тогда  // не округляем мелкие объемы И МАТЕРИАЛЫ и в формате 2018
				НовСтр.Объем =  Окр(НовСтр.НормаРасхода  *  ТекущийЭлементДерева.Объем, 6);
			КонецЕсли;
			
			
		иначеЕсли флФорматСметы = 1 Тогда  // норма как общий объем на работу
			
			Если ТекущийЭлементДерева.Объем <> 0 Тогда
				НовСтр.Объем =  СтрокаВЧисло(ЭлементСпискаХДТО.Норма);
				НовСтр.НормаРасхода = СтрокаВЧисло(ЭлементСпискаХДТО.Норма) / ТекущийЭлементДерева.Объем;					
			иначе
				НовСтр.НормаРасхода = 0;
			КонецЕсли;
			
		КонецЕсли;
		
		
		
	КонецЕсли;
	
	//2 РАСЧЕТ СУММЫ  и ЦЕНЫ
	
	Если ЭлементСпискаХДТО.Свойства().Получить("Цена") <> Неопределено Тогда
		
		НовСтр.Цена =  СтрокаВЧисло(ЭлементСпискаХДТО.Цена);
		
		Если  флФорматСметы = 2 Тогда // формат 2018 не округляем
			НовСтр.Сумма = Окр(НовСтр.Объем	* НовСтр.Цена, 2);
		иначе	
			НовСтр.Сумма = Окр(НовСтр.Объем	* НовСтр.Цена, 0);
		КонецЕсли;
	КонецЕсли;
	
	Если ТекущийЭлементДерева.Тип = "6" // Машинные услуги (?)
		или ТекущийЭлементДерева.Тип = "1" //материал
		или ТекущийЭлементДерева.Тип = "2" // оборудование
		

		Тогда 
		// Работы-ресурсы - берем данные из работы и трансформируем
		
		НовСтр.Сумма = ТекущийЭлементДерева.Сумма;
		
		Если ТекущийЭлементДерева.Объем <> 0 Тогда
			НовСтр.Цена  = ТекущийЭлементДерева.Сумма/ ТекущийЭлементДерева.Объем;	
		КонецЕсли;
		
		Если ТекущийЭлементДерева.Тип = "6" Тогда
			НовСтр.Тип = "3";
		ИначеЕсли ТекущийЭлементДерева.Тип = "1" Тогда
			НовСтр.Тип = "2";
		ИначеЕсли ТекущийЭлементДерева.Тип = "2" Тогда 
			НовСтр.Тип = "3";
		Иначе
			НовСтр.Тип = "2";
		КонецЕсли;

		
		НовСтр.НормаРасхода =  1;
		
	КонецЕсли;
	
	Если ЭлементСпискаХДТО.Свойства().Получить("ЗМ") <> Неопределено Тогда
		
		Если  флФорматСметы = 2 Тогда // формат 2018 не округляем
			НовСтр.СуммаЗПМашинистов = Окр(НовСтр.Объем	* СтрокаВЧисло(ЭлементСпискаХДТО.ЗМ), 2);
		иначе
			НовСтр.СуммаЗПМашинистов = Окр(НовСтр.Объем	* СтрокаВЧисло(ЭлементСпискаХДТО.ЗМ), 0);
		КонецЕсли;
		
		
	КонецЕсли;
	
	
	// по машинистам в файле нет суммовых показателей - на практике такой ресурс один 
	// возьмем стоимость из самых итогов работы
	ЗарплатаМашинистов = Ложь;
	Если НовСтр.Название = "Затраты труда машинистов" Тогда
		
		НовСтр.Сумма  = ТекущийЭлементДерева.СуммаЗПМашинистов; // Родитель это работа в целом	
		//НовСтр.СуммаЗПМашинистов = ТекущийЭлементДерева.СуммаЗПМашинистов;
		ЗарплатаМашинистов = Истина;
		
		Если НовСтр.Объем <> 0 Тогда
			НовСтр.Цена  = НовСтр.Сумма/ НовСтр.Объем;	
		КонецЕсли;
		
	КонецЕсли;
	
	// вызов создания/поиска ресурса сметы
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Шифр", НовСтр.КодСНБ);
	СтруктураПараметров.Вставить("Наименование", НовСтр.Название);
	СтруктураПараметров.Вставить("ЕдиницаИзмерения", НовСтр.ЕдиницаИзмерения);
	СтруктураПараметров.Вставить("тип", НовСтр.тип);
	СтруктураПараметров.Вставить("РодительСсылка", РодительСсылка);
	СтруктураПараметров.Вставить("АвтосозданиеНедостающихРесурсов", АвтосозданиеНедостающихРесурсов);
	СтруктураПараметров.Вставить("ЭтоЗарплатаМашинистов", ЗарплатаМашинистов);
	СтруктураПараметров.Вставить("ЭтоОборудование", ТекущийЭлементДерева.Тип = "2");
 	
	НовСтр.ВидРаботы =  ЗаполнитьРесурс(СтруктураПараметров, КэшРесурсов);
	
	ДобавитьРесурс(НовСтр, ТекущийЭлементДерева.ВидРаботы);  // доптаблица с ресурсами		
	
КонецФункции



&НаСервереБезКонтекста
Функция ЗаполнитьСтоимости(ЭлементСпискаХДТО, НовСтр, ПоказательСП, флФорматСметы)
	
	Если ЭлементСпискаХДТО.Свойства().Получить("СТОИМОСТЬ") <> Неопределено Тогда
		
		СвойстваСтоимости =  ЭлементСпискаХДТО.СТОИМОСТЬ.ВСЕГО;
		
 		// СТОИМОСТИ
		
		Если  СвойстваСтоимости.Свойства().Получить("ПЗ") <> Неопределено  Тогда
			НовСтр.Сумма =  СтрокаВЧисло(СвойстваСтоимости.ПЗ);
		иначеЕсли СвойстваСтоимости.Свойства().Получить("ОТП") <> Неопределено  Тогда  //оборудование нет прямых затрат
			НовСтр.Сумма =  СтрокаВЧисло(СвойстваСтоимости.ОТП);
		КонецЕсли;
		
				
		Если  СвойстваСтоимости.Свойства().Получить("ЗП") <> Неопределено  Тогда
			НовСтр.СуммаЗП =  СтрокаВЧисло(СвойстваСтоимости.ЗП);
 		КонецЕсли;
		
		//Если НовСтр.Сумма = 0 Тогда 
		//	
		//	// иногда почему то нет стоимости в секции  СТОИМОСТЬ но есть в ЕДИНИЦА
		//	
		//	СвойстваСтоимостиЕд =  ЭлементСпискаХДТО.СТОИМОСТЬ.ЕДИНИЦА;	
		//	
		//	Если  СвойстваСтоимостиЕд.Свойства().Получить("ПЗ") <> Неопределено  Тогда
		//		НовСтр.Сумма =  СтрокаВЧисло(СвойстваСтоимости.ПЗ) * НовСтр.Объем;
		//	КонецЕсли;
		//	
		//	
		//КонецЕсли;
		//
 		
		Если  СвойстваСтоимости.Свойства().Получить("НК1") <> Неопределено  Тогда
			НовСтр.НакладныеРасходы = СтрокаВЧисло(СвойстваСтоимости.НК1);
		КонецЕсли;
		
		Если  СвойстваСтоимости.Свойства().Получить("НК2") <> Неопределено  Тогда
			НовСтр.НакладныеРасходыРаспред = СтрокаВЧисло(СвойстваСтоимости.НК2);
		КонецЕсли;
		
		
 		
		Если  СвойстваСтоимости.Свойства().Получить("ЗМ") <> Неопределено  Тогда
			НовСтр.СуммаЗПМашинистов =  Окр(СтрокаВЧисло(СвойстваСтоимости.ЗМ), 0);
		КонецЕсли;
		
 		
		Если  СвойстваСтоимости.Свойства().Получить("СР") <> Неопределено  Тогда
			НовСтр.СР =  СтрокаВЧисло(СвойстваСтоимости.СР); //разряд?
		КонецЕсли;

		
		
		Если  СвойстваСтоимости.Свойства().Получить("СП") <> Неопределено  Тогда
			
			НовСтр.СметнаяПрибыль =  СтрокаВЧисло(СвойстваСтоимости.СП);
			
		Иначе
			// Работа-ресурс  по ним в файле нет сметной прибыли
			// рассчитаем
			
			Если ПоказательСП <> 0 Тогда
				НовСтр.СметнаяПрибыль = (НовСтр.Сумма + НовСтр.НакладныеРасходы + НовСтр.НакладныеРасходыРаспред) * ПоказательСП / 100;		
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПоказательСП = 0 и НовСтр.СметнаяПрибыль <> 0  Тогда
			ПоказательСП = окр(НовСтр.СметнаяПрибыль / (НовСтр.Сумма + НовСтр.НакладныеРасходы + НовСтр.НакладныеРасходыРаспред), 2) * 100; 	 // рассчитаем для строк где не указана в файле
		КонецЕсли;
		
		
		Если НовСтр.Объем < 0 Тогда
			НовСтр.Сумма =  ?(НовСтр.Сумма> 0, - НовСтр.Сумма, НовСтр.Сумма);
			НовСтр.СметнаяПрибыль =   ?(НовСтр.СметнаяПрибыль> 0, - НовСтр.СметнаяПрибыль, НовСтр.СметнаяПрибыль);
		КонецЕсли;
		
		
		Если флФорматСметы=2 Тогда // накладные расходы надо рассчитать
			
 			НовСтр.НакладныеРасходыРаспред =    Окр(СтрокаВЧисло(СвойстваСтоимости.ВСЕГО) - НовСтр.Сумма - НовСтр.СметнаяПрибыль, 0);
			
		КонецЕсли;

 		
	КонецЕсли;
	
КонецФункции



 &НаСервере
Процедура СоздатьУГПР(ДанныеФормы, ДополнительныеПараметры = Неопределено)
	
	Перем Организация, НаименованиеВерсии, СтруктураУГПР, ТаблицаСоответствий, НовыйДок;
	
 	Организация = ИНАГРО_ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.Проект, "ИНАГРО_Организация");
	
 	Если ДополнительныеПараметры <> Неопределено Тогда
		НовыйДок = ДополнительныеПараметры.Скопировать();
		//НаименованиеВерсии = ИНАГРО_ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДополнительныеПараметры.Версия, "Наименование");
		//НаименованиеВерсии = ""+НаименованиеВерсии + "+ Загрузка из 1с файла от "+ТекущаяДатаСеанса();
 	иначе
		НовыйДок = Документы.ИНАГРО_УГПР.СоздатьДокумент();
		//НаименованиеВерсии = "Загрузка из 1с сметы от "+ТекущаяДатаСеанса();
	КонецЕсли; //Если ДополнительныеПараметры <> Неопределено Тогда

	
	
	//НовыйДок.Версия = СоздатьВерсиюДляЗагрузки(НаименованиеВерсии, Организация, Объект.Проект);
	
	НовыйДок.Дата = ТекущаяДата();
	НовыйДок.УстановитьНовыйНомер();
	НовыйДок.Статус = Перечисления.СтатусыПланов.ВПодготовке;
	НовыйДок.Проект = Объект.Проект;
	НовыйДок.Организация = Организация;
	НовыйДок.Комментарий =  "Загружено из файла";
	
	
	// 1 определяем Работы	
 	ВыгрузитьДеревоЗначенийВТаблицуЗначений(РеквизитФормыВЗначение("РаботыДЗ"), НовыйДок.Работы);
	
	// 2 заполняем ресурсы 
	
	Для каждого ТекСтрРес  Из ТаблицаРесурсов Цикл
			
			ИмяТЧ = ИмяТЧПоВидуРесурса(ТекСтрРес.ТипРесурса);
			
			тчТаблицаРесурсов = НовыйДок[ИмяТЧ];
			
			//измерения
			НС = тчТаблицаРесурсов.Добавить();
			
			ЗаполнитьЗначенияСвойств(НС, ТекСтрРес);
  			
 			НС.Количество = ТекСтрРес.Объем;
 			
			Если ИмяТЧ = "РесурсыМашинные" Тогда
				
			КонецЕсли;
			
 			////Пересчет норм
			//Если ОбъемРабот <> 0 Тогда
			//	НС.НормаРасхода = НС.Количество / ОбъемРабот;	
			//КонецЕсли;
			
			
			
			
		КонецЦикла; //Для каждого ТекСтрРес  Из таблРесурсов Цикл

	
	// 3 - операции  
	
	
	
	//НовыйДок.СметнаяПрибыль = ИтогоПоСмете  * ПоказательСП / 100;
  	
	ЗначениеВДанныеФормы(НовыйДок, ДанныеФормы);
	
	НовыйДок.Дата = ТекущаяДата();
	
КонецПроцедуры
 








// ВСПОМОГАТЕЛЬНЫЕ
&наКлиентеНаСервереБезКонтекста
Функция ЭтоЧисло_Найти(Слово)
	
	Цифры = "1234567890-"; // числа + символ "-"
	
	Для НомСимвола = 1 По СтрДлина(Слово) Цикл
		
		Если Найти(Цифры, Сред(Слово, НомСимвола, 1)) = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина
	
КонецФункции


&НаСервере
Функция СкорректироватьОплатуМашинистов(Ветка_Работа)
	Перем ЭлементОсновнаяЗП;
	// для случаев когда  в смете заложены надбавки к затратам труда машиниста 
	//и не включены в стоимость основных ресурсов - в данном случае в стоимость машин
	//нужна корретировка
	
	Если Ветка_Работа.СуммаЗПМашинистов > 0 Тогда
		
		тчРесурсы = Ветка_Работа.ПолучитьЭлементы();
		
		Если тчРесурсы.Количество()   Тогда
			
			ИтогСуммаЗПМашинистов = 0;
			
			МассивРесурсовСЗарплатой = новый Массив;
			
			Для каждого ЭлементДерева Из тчРесурсы Цикл
				
				Если ЭлементДерева.СуммаЗПМашинистов> 0 Тогда
					ИтогСуммаЗПМашинистов = ИтогСуммаЗПМашинистов + ЭлементДерева.СуммаЗПМашинистов;
					МассивРесурсовСЗарплатой.Добавить(ЭлементДерева);
				КонецЕсли;
				
				//TODO других вариантов пока не видно - только по названию или набору шифров 
				Если ЭлементДерева.Название = "Затраты труда рабочих-строителей" Тогда
					ЭлементОсновнаяЗП = ЭлементДерева;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ИтогСуммаЗПМашинистов > 0 и  ИтогСуммаЗПМашинистов < Ветка_Работа.СуммаЗПМашинистов Тогда
				
				РазницаКРаспределению =  Ветка_Работа.СуммаЗПМашинистов - ИтогСуммаЗПМашинистов;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Распределена разница "+РазницаКРаспределению+  " по работе "+Ветка_Работа.Название + " (код WBS "+Ветка_Работа.КодWBS+")");
 				
				Для каждого ЭлементДерева Из МассивРесурсовСЗарплатой Цикл
					
					Разница =  Окр(РазницаКРаспределению / ИтогСуммаЗПМашинистов * ЭлементДерева.СуммаЗПМашинистов, 0);	
					
					ИтогСуммаЗПМашинистов = ИтогСуммаЗПМашинистов - ЭлементДерева.СуммаЗПМашинистов;
					
					ЭлементДерева.Сумма	 = 	ЭлементДерева.Сумма + Разница;
					ЭлементДерева.СуммаЗПМашинистов	 = 	ЭлементДерева.СуммаЗПМашинистов + Разница;
					
					
					РазницаКРаспределению = РазницаКРаспределению - Разница;
					
					НайдСтроки = ТаблицаРесурсов.НайтиСтроки(Новый Структура("КлючСтроки, Ресурс", ЭлементДерева.КлючСтроки, ЭлементДерева.ВидРаботы));
					
					Для каждого СтрокаРесурс Из НайдСтроки Цикл
						СтрокаРесурс.Сумма = ЭлементДерева.Сумма;
						СтрокаРесурс.СуммаЗПМашинистов = ЭлементДерева.СуммаЗПМашинистов;
					КонецЦикла;
					
 				КонецЦикла;
				
			КонецЕсли; //Если ИтогСуммаЗПМашинистов > 0 
			
			//Проверим и СМР обычно требуют корректировки в тот же момент
			
			Если Ветка_Работа.СуммаЗП > 0 
				и ЭлементОсновнаяЗП <> Неопределено  
				и ЭлементОсновнаяЗП.Сумма <  Ветка_Работа.СуммаЗП Тогда
				
				
				ЭлементОсновнаяЗП.Сумма = Ветка_Работа.СуммаЗП;
				
				НайдСтроки = ТаблицаРесурсов.НайтиСтроки(Новый Структура("КлючСтроки, Ресурс", ЭлементОсновнаяЗП.КлючСтроки, ЭлементОсновнаяЗП.ВидРаботы));
				
				Для каждого СтрокаРесурс Из НайдСтроки Цикл
					СтрокаРесурс.Сумма = ЭлементОсновнаяЗП.Сумма;
 				КонецЦикла;
				
				
			КонецЕсли;
			
			
			
		КонецЕсли; //Если тчРесурсы.Количество()   Тогда

		
 	КонецЕсли; //Если Ветка_Работа.СуммаЗПМашинистов > 0 Тогда
	
КонецФункции

 


&НаСервереБезКонтекста
Функция НайтиСоздатьединицуИзмерения_смета(Наименование)
	
	Перем ЕдиницаИзмерения;
	
	ЕдиницаИзмерения = СМ_СметаВызовСервера.НайтиЕдиницуИзмеренияПоСтроке(Наименование);
	
	Если ЕдиницаИзмерения.Пустая() Тогда
		Единица = Справочники.СМ_ЕдиницыИзмеренияСмета.СоздатьЭлемент();
		Единица.Наименование = Наименование;
		Единица.ОбменДанными.Загрузка = Истина;;
		Единица.Код = "000";
		Единица.Записать();
		ЕдиницаИзмерения = Единица.Ссылка;
	КонецЕсли;
	
	Возврат ЕдиницаИзмерения;
	
КонецФункции

 
&НаСервере
Функция ДобавитьРесурс(СтрокаИзДерева, ВидРаботыРодитель)
	
	НовСтр =  ТаблицаРесурсов.Добавить();
	ЗаполнитьЗначенияСвойств(НовСтр, СтрокаИзДерева);
	
	НовСтр.Ресурс =  СтрокаИзДерева.ВидРаботы;
	НовСтр.ВидРаботы = ВидРаботыРодитель;
	//НовСтр.ТипРесурса = ИНАГРО_ОбщегоНазначения.ПолучитьЗначениеРеквизита(НовСтр.Ресурс, "ИНАГРО_ТипРесурсов");
	
	МассивСтрок =  КэшРесурсов.НайтиСтроки(Новый Структура("Код", СтрокаИзДерева.КодСНБ));
	
	Для каждого СтрокаМассива Из МассивСтрок Цикл
		НовСтр.ТипРесурса = СтрокаМассива.ИНАГРО_ТипРесурсов;
	КонецЦикла;
 		
	Если Не ЗначениеЗаполнено(НовСтр.ТипРесурса)  Тогда // в формате биай?
		
		МассивСтрок =  КэшРесурсов.НайтиСтроки(Новый Структура("Код",  СтрокаИзДерева.КодСНБ+"-00"));
		
		Для каждого СтрокаМассива Из МассивСтрок Цикл
			НовСтр.ТипРесурса = СтрокаМассива.ИНАГРО_ТипРесурсов;
		КонецЦикла;
		
	КонецЕсли;
	
 	
	//
	//Если  НовСтр.Объем <> 0 Тогда
	//	НовСтр.Цена = НовСтр.Сумма / НовСтр.Объем;	
	//КонецЕсли;	
	

КонецФункции


&НаСервере
Функция ЗаполнитьНормативныйСборник(Шифр)	
	Перем Рез;
	
	//КэшНормативныхСборников.Сбросить();
	
	//Если КэшНормативныхСборников.НайтиСледующий(Новый Структура("КодНормативнойРасценки", Шифр)) Тогда
	//	Рез = КэшНормативныхСборников.Ссылка;		
	//КонецЕсли;
	
	МассивСтрок =  КэшНормативныхСборников.НайтиСтроки(Новый Структура("КодНормативнойРасценки", Шифр));
	
	Для каждого СтрокаМассива Из МассивСтрок Цикл
		Рез = СтрокаМассива.Ссылка;
	КонецЦикла;
	
	
	Если  Рез = Неопределено  Тогда // в формате биай?
		
		МассивСтрок =  КэшНормативныхСборников.НайтиСтроки(Новый Структура("КодНормативнойРасценки",  Шифр+"-00"));
		
		Для каждого СтрокаМассива Из МассивСтрок Цикл
			Рез = СтрокаМассива.Ссылка;
		КонецЦикла;
		
	КонецЕсли;

	
	Возврат Рез;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьРесурс(СтруктураПараметров, КэшРесурсов)	
	Перем Рез;
	
	
	Если ЗначениеЗаполнено(СтруктураПараметров.Шифр) и ЭтоЧисло_Найти(СтруктураПараметров.Шифр) Тогда
		
 		МассивСтрок =  КэшРесурсов.НайтиСтроки(Новый Структура("Код", СтруктураПараметров.Шифр));
		
		Для каждого СтрокаМассива Из МассивСтрок Цикл
			Рез = СтрокаМассива.Ссылка;
		КонецЦикла;
		
		Если  Рез = Неопределено  Тогда // в формате биай?
			
			МассивСтрок =  КэшРесурсов.НайтиСтроки(Новый Структура("Код",  СтруктураПараметров.Шифр+"-00"));
			
			Для каждого СтрокаМассива Из МассивСтрок Цикл
				Рез = СтрокаМассива.Ссылка;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
		
	Если Рез = Неопределено тогда
		// поиск по наименованию
		
		МассивСтрок =  КэшРесурсов.НайтиСтроки(Новый Структура("Наименование",  Лев(СтруктураПараметров.Наименование,1024)));
		
		Для каждого СтрокаМассива Из МассивСтрок Цикл
			Рез = СтрокаМассива.Ссылка;
		КонецЦикла;

		
	КонецЕсли;
	
	Если  Рез = Неопределено и  СтруктураПараметров.АвтосозданиеНедостающихРесурсов Тогда
		
		ТекРесурсОбъект 					= Справочники.СМ_Ресурсы.СоздатьЭлемент();
 		
		Если  СтруктураПараметров.Тип = "1" Тогда  // рабочие и машинисты
			
			Если СтруктураПараметров.ЭтоЗарплатаМашинистов Тогда
				ТекРесурсОбъект.ВидРесурса = Перечисления.СМ_ВидыРесурсов.ЗатратыТрудаМашинистов;
			Иначе
				ТекРесурсОбъект.ВидРесурса = Перечисления.СМ_ВидыРесурсов.ЗатратыТрудаРабочих;
			КонецЕсли;
			
			
		ИначеЕсли  СтруктураПараметров.Тип = "2" Тогда  // Материалы
			ТекРесурсОбъект.ВидРесурса = Перечисления.СМ_ВидыРесурсов.Материалы;
		ИначеЕсли  СтруктураПараметров.Тип = "3" Тогда  // машины и механизмы
			
			Если СтруктураПараметров.ЭтоОборудование Тогда
				ТекРесурсОбъект.ВидРесурса = Перечисления.СМ_ВидыРесурсов.Оборудование;
			иначе
				ТекРесурсОбъект.ВидРесурса = Перечисления.СМ_ВидыРесурсов.МашиныИМеханизмы;
			КонецЕсли;
			
			
		КонецЕсли;
		
		
		Если ЭтоЧисло_Найти(СтруктураПараметров.Шифр) Тогда
			ТекРесурсОбъект.Код 				=  СтруктураПараметров.Шифр;
		иначе
			ТекРесурсОбъект.ОбменДанными.Загрузка = истина;
		КонецЕсли;
 		
 		ТекРесурсОбъект.Наименование 		=  СтруктураПараметров.Наименование;
		ТекРесурсОбъект.ДлинноеНаименование =  СтруктураПараметров.Наименование;
		ТекРесурсОбъект.Родитель 			=  СтруктураПараметров.РодительСсылка;
 		
		ТекРесурсОбъект.ЕдиницаИзмерения =  СтруктураПараметров.ЕдиницаИзмерения;
		
		// TODO уточнить 
		ТекРесурсОбъект.ГодВыпуска 	     = Перечисления.СМ_ГодыВыпускаБазы.г2017;
		ТекРесурсОбъект.ГруппаРесурсов = Справочники.СМ_ГруппыРесурсов.КаталогBiGroup;
		
		Попытка
			
			
			
			ТекРесурсОбъект.Записать();
			
			НовЗаписьКэша = КэшРесурсов.Добавить();
			НовЗаписьКэша.Код =  СтруктураПараметров.Шифр;
			НовЗаписьКэша.Ссылка = ТекРесурсОбъект.Ссылка;
			НовЗаписьКэша.ИНАГРО_ТипРесурсов = ТекРесурсОбъект.ИНАГРО_ТипРесурсов;
			НовЗаписьКэша.Наименование =  Лев(ТекРесурсОбъект.ДлинноеНаименование, 1024);
 		Исключение
 			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Элемент справочника Ресурсы не записан:'")+" " + ОписаниеОшибки());
		КонецПопытки;
		
		Рез = ТекРесурсОбъект.Ссылка;
		
	КонецЕсли;
	
 	
	Возврат Рез;
	
КонецФункции
  
  
&НаСервереБезКонтекста
Функция СтрокаВЧисло(ИсходнаяСтрока)
	
	Возврат	ИНАГРО_УСПОбщиеФункцииКлиентСервер.СтрокаВЧислоПривести(ИсходнаяСтрока);
	
КонецФункции
 

 &НаСервере
Процедура ПеренестивУГПРНаСервере(ДанныеФормы, ДополнительныеПараметры)
	
	Если РаботыДЗ.ПолучитьЭлементы().Количество() Тогда
		
		СоздатьУГПР(ДанныеФормы, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыгрузитьДеревоЗначенийВТаблицуЗначений(Дерево, Таблица = Неопределено) Экспорт
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Если СтрокаДерева.ЭтоРесурс Тогда
			Продолжить;
		КонецЕсли;
		
		НС = Таблица.Добавить();
		
 		ЗаполнитьЗначенияСвойств(НС, СтрокаДерева);
		
		
		Если не СтрокаДерева.ЭтоРабота Тогда
			НС.КороткоеИмя = СтрокаДерева.КодСНБ;
		иначе	
		
		КонецЕсли;
		
		НС.Сумма = 0; // стоимость заказчика не трогаем	
		
		ВыгрузитьДеревоЗначенийВТаблицуЗначений(СтрокаДерева, Таблица);
		
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции //ВыгрузитьДеревоЗначенийВТаблицуЗначений()

&НаСервереБезКонтекста
Функция НайтиРаботу(НовСтр, Проект, КэшРаботПроекта, СоздаватьАвто = Ложь)
	Перем Рез;
	
	Если не ЗначениеЗаполнено(Проект) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(НовСтр.КодСНБ) и ЭтоЧисло_Найти(НовСтр.КодСНБ) Тогда
		// по коду
		
		МассивСтрок =  КэшРаботПроекта.НайтиСтроки(Новый Структура("ШифрПозицииНорматива", НовСтр.КодСНБ));
		
		Для каждого СтрокаМассива Из МассивСтрок Цикл
			Рез = СтрокаМассива.Ссылка;
		КонецЦикла;
		
	КонецЕсли;

 		
				
	Если  Рез = Неопределено  Тогда
		// по наименованию
		
 		МассивСтрок =  КэшРаботПроекта.НайтиСтроки(Новый Структура("Наименование", НовСтр.Название));
		
		Для каждого СтрокаМассива Из МассивСтрок Цикл
			Рез = СтрокаМассива.Ссылка;
		КонецЦикла;
		
  		
	КонецЕсли;
	
	
	
	Если Рез = Неопределено и СоздаватьАвто Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Наименование", НовСтр.Название);
		ПараметрыФормы.Вставить("Владелец", Проект);
		ПараметрыФормы.Вставить("ЕдиницаИзмерения", НовСтр.ЕдиницаИзмерения);
 		ПараметрыФормы.Вставить("ШифрПозицииНорматива", НовСтр.КодСНБ);
		
	 	Если ЗначениеЗаполнено(НовСтр.НормативныйСборник) Тогда
			ПараметрыФормы.Вставить("НормативныйСборник", НовСтр.НормативныйСборник);
		КонецЕсли;
		
		Рез =  Справочники.ИНАГРО_ВидыРаботПроект.АвтосозданиеВидаРаботы(ПараметрыФормы);
		
		Если ЗначениеЗаполнено(Рез) Тогда
			новСтрКеша = КэшРаботПроекта.Добавить();
			новСтрКеша.ШифрПозицииНорматива = НовСтр.КодСНБ;
			новСтрКеша.Ссылка =  Рез;
			новСтрКеша.Наименование =  НовСтр.Название;
		КонецЕсли;
		
	КонецЕсли;

	
	
	Возврат Рез;

	
КонецФункции

 &НаСервереБезКонтекста
Функция НайтиКонструктив(НовСтр, Проект,  СоздаватьАвто = Ложь)
	 
	 Если не ЗначениеЗаполнено(Проект) Тогда
		Возврат Неопределено;
	КонецЕсли;

	 
	 ПараметрыФормы = Новый Структура;
	 ПараметрыФормы.Вставить("Наименование", НовСтр.Название);
	 ПараметрыФормы.Вставить("Владелец", Проект);
	 ПараметрыФормы.Вставить("УровеньСметы", Перечисления.ИНАГРО_УровеньСметы.РазделСметы);
	 
	 Возврат Справочники.ИНАГРО_КонструктивыПроекта.НайтиПоПараметрам(ПараметрыФормы, СоздаватьАвто);
	 
КонецФункции

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АвтосозданиеWBS1c = Истина;
	АвтозаполнениеНормативныхСборников = Истина;
	АвтосозданиеНедостающихРесурсов = Истина;
	
	
		
	
КонецПроцедуры
    
 &НаСервереБезКонтекста
Функция ИмяТЧПоВидуРесурса(ВидРесурса)
	
	ТаблицаРесурсов = "РесурсыМатериалы";
	
	Если ВидРесурса = Перечисления.ИНАГРО_ТипыРесурсов.ТрудовыеРесурсы  Тогда
		
		ТаблицаРесурсов = "РесурсыТрудовые";
		
	иначеЕсли ВидРесурса = Перечисления.ИНАГРО_ТипыРесурсов.МашиныИМеханизмы Тогда
		
		ТаблицаРесурсов = "РесурсыМашинные";
		
	КонецЕсли;
	
	Возврат  ТаблицаРесурсов;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьКэшНормативныхСборников()

 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СМ_НормативныеСборники.Ссылка,
		|	СМ_НормативныеСборники.КодНормативнойРасценки
		|ИЗ
		|	Справочник.СМ_НормативныеСборники КАК СМ_НормативныеСборники
		|ГДЕ
		|	НЕ СМ_НормативныеСборники.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	ВыборкаДетальныеЗаписи.Индексы.Добавить("КодНормативнойРасценки");
	
	
	Возврат ВыборкаДетальныеЗаписи;
 
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьКэшРесурсов()

 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СМ_Ресурсы.Ссылка,
		|	СМ_Ресурсы.Код,
		|	СМ_Ресурсы.ИНАГРО_ТипРесурсов,
		|	ВЫРАЗИТЬ(СМ_Ресурсы.ДлинноеНаименование КАК СТРОКА(1024)) КАК Наименование
		|ИЗ
		|	Справочник.СМ_Ресурсы КАК СМ_Ресурсы
		|ГДЕ
		|	НЕ СМ_Ресурсы.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	ВыборкаДетальныеЗаписи.Индексы.Добавить("Код");
	ВыборкаДетальныеЗаписи.Индексы.Добавить("Наименование");

	
	Возврат ВыборкаДетальныеЗаписи;
 
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьКэшРабот(Проект)

 	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИНАГРО_ВидыРаботПроект.Ссылка,
		|	ИНАГРО_ВидыРаботПроект.НаименованиеПолное как Наименование,
		|	ИНАГРО_ВидыРаботПроект.ШифрПозицииНорматива
		|ИЗ
		|	Справочник.ИНАГРО_ВидыРаботПроект КАК ИНАГРО_ВидыРаботПроект
		|ГДЕ
		|	ИНАГРО_ВидыРаботПроект.Владелец = &Проект";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	ВыборкаДетальныеЗаписи.Индексы.Добавить("ШифрПозицииНорматива");
	ВыборкаДетальныеЗаписи.Индексы.Добавить("Наименование");
	
	Возврат ВыборкаДетальныеЗаписи;
 
	
КонецФункции


&НаСервереБезКонтекста
Процедура ПроектПриИзмененииНаСервере(Проект, АвтосозданиеНедостающихРесурсов, РодительСсылка)
	
	Если АвтосозданиеНедостающихРесурсов Тогда
		
 		// Папка для добавления новых ресурсов
		НаименованиеРодителя = "Ресурсы проекта " + СокрЛП(Проект);
		РодительСсылка = Справочники.СМ_Ресурсы.НайтиПоНаименованию(НаименованиеРодителя, Истина, Справочники.СМ_Ресурсы.Инагро_РесурсыПроектов);
		Если РодительСсылка.Пустая() ИЛИ НЕ РодительСсылка.ЭтоГруппа Тогда
			РодительОбъект = Справочники.СМ_Ресурсы.СоздатьГруппу();
			РодительОбъект.Наименование = НаименованиеРодителя;
			РодительОбъект.Родитель = Справочники.СМ_Ресурсы.Инагро_РесурсыПроектов;
			РодительОбъект.ОбменДанными.Загрузка = Истина;
			РодительОбъект.Записать();
			РодительСсылка = РодительОбъект.Ссылка;
		КонецЕсли;
		
		
	КонецЕсли;


КонецПроцедуры


&НаСервере
Процедура ИзменитьВерсиюСметы()
	
	//5.5.6(0)  - нормы указаны как нормы на единицу
	//5.5.1(1)  - нормы указаны как объемы на работу
	//2018.1(2) - накладные расходы не указаны в файле, нужен расчет от общей стоимости на разницу(?)
	
	Если ВерсияГенератора = "5.5.6" Тогда
		флФорматСметы = 0;
	ИначеЕсли ВерсияГенератора = "5.5.1" Тогда
		флФорматСметы = 1;
	ИначеЕсли ВерсияГенератора = "5.5.2" Тогда
		флФорматСметы = 0;
	ИначеЕсли ВерсияГенератора = "2018.1" или ВерсияГенератора = "2018" Тогда
		флФорматСметы = 2;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область Клиент


&НаКлиенте
Процедура ПеренестивУГПР(Команда)
 		
	Если не ЗначениеЗаполнено(Объект.Проект) Тогда
		ПоказатьПредупреждение(, "Не выбран проект!");
		Возврат;
	КонецЕсли;

 	Форма = ПолучитьФорму("Документ.ИНАГРО_УГПР.Форма.ФормаДокумента");
	
	ДанныеФормы = Форма.Объект;
	
	ПеренестивУГПРНаСервере(ДанныеФормы, Неопределено);
	
	КопироватьДанныеФормы(ДанныеФормы, Форма.Объект);
	
  	Форма.Открыть();
	Форма.РаботыДЗОбновитьНаСервере(); // 1 последовательность важна  - создаем дерево из таблиц документа
	//Форма.РаспределитьНРиПрибыльНаСервере(); // 2 

КонецПроцедуры

 &НаКлиенте
Процедура РазвернутьВсе(Команда)
	ИНАГРО_УСПОбщиеФункцииКлиентСервер.РазвернутьВсе(РаботыДЗ, Элементы.ДеревоРаботСметы);
КонецПроцедуры
 
&НаКлиенте
Процедура СвернутьВсе(Команда)
	ИНАГРО_УСПОбщиеФункцииКлиентСервер.СвернутьВсе(РаботыДЗ, Элементы.ДеревоРаботСметы);
КонецПроцедуры

 &НаКлиенте
 Процедура ВыбратьФайл(Команда)
	 
	 Если не ЗначениеЗаполнено(Объект.Проект) Тогда
		 ПоказатьПредупреждение(, "Не выбран проект!");
		 Возврат;
	 КонецЕсли;
	 
	 
	 //для веб-клиента требуется дополнительно подключение расширения работы с файлами
	 #Если ВебКлиент Тогда
		 Результат = Неопределено;
		 
		 НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПрочитатьФайлЗавершение2", ЭтаФорма));
		 Возврат;
	 #КонецЕсли
	 
	 //перемещаем файл на сервер во "внутреннее хранилище сервера"
	 ПрочитатьФайлФрагмент1();
	 
КонецПроцедуры

 &НаКлиенте
Процедура ПрочитатьФайлЗавершение2(Подключено, ДополнительныеПараметры) Экспорт
	
	Результат = Подключено;
	
	//если подключить не удалось - требуется установить его (в первый раз)
	Если не Результат Тогда
		НачатьУстановкуРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПрочитатьФайлЗавершение1", ЭтаФорма));
		Возврат;
	КонецЕсли;
	
	ПрочитатьФайлФрагмент1();

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлФрагмент1()
	
	ПрочитатьФайлФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлЗавершение1(ДополнительныеПараметры) Экспорт
	
	//устанавливаем
	
	//если не удалось установить - то повторный вызов подключения будет неудачный
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПрочитатьФайлЗавершение", ЭтаФорма));

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если не Подключено Тогда
		//сообщаем об ошибке и прерываем работу программы
		ВызватьИсключение "Ошибка. Ваш браузер не поддерживает работу с файлами.";
	КонецЕсли;
	
	ПрочитатьФайлФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлФрагмент()
	
	Перем ВнутреннийАдресСервера, ВыборФайла, ОписаниеОповещения;
	
	ВнутреннийАдресСервера = "";
  	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.Заголовок                 = "Выберите файлы для загрузки";
	ВыборФайла.Фильтр                     = "Файл (kenml)|*.kenml";
	ВыборФайла.МножественныйВыбор = Истина;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьФайл_Завершение", ЭтаФорма);
	
	Состояние("Идет чтение файла...");
	
	НачатьПомещениеФайлов(ОписаниеОповещения, , ВыборФайла , Истина, Новый УникальныйИдентификатор);
	
 
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайл_Завершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ПомещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаботыДЗ.ПолучитьЭлементы().Очистить();
	ТаблицаРесурсов.Очистить();

	ИтогоПоСмете = 0;
	

	К = 0;
	Для каждого ПереданныйФайл Из ПомещенныеФайлы Цикл
		
		К= К+1;
		
		Состояние("Обработка файла ("+К+") из (" +ПомещенныеФайлы.Количество()+")", 33);

		
		ПутьКФайлу = ПереданныйФайл.Имя;
 		ВнутреннийАдресСервера = ПереданныйФайл.Хранение;
		
		ЧтениеФайла(ВнутреннийАдресСервера);

	КонецЦикла;

	
 	 
	ИНАГРО_УСПОбщиеФункцииКлиентСервер.РазвернутьВсе(РаботыДЗ, Элементы.ДеревоРаботСметы);
	
	Состояние("Обработка файла завершена", 100);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРаботСметыПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущееПоле = Элемент.ТекущийЭлемент;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьДоступностьРеквизитов(Элемент, ТекущиеДанные, ТекущееПоле);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьРеквизитов(Элемент, ТекущиеДанные, ТекущееПоле)
	
	Если ТекущееПоле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоКонструктив = не ТекущиеДанные.ЭтоРабота;
	
	СтруктураТолькоЧтение = Новый Структура;
 	СтруктураТолькоЧтение.Вставить("ДеревоРаботСметыНормативныйСборник", ЭтоКонструктив);
 
	
	
	ТолькоЧтение = Истина;
	
	Если СтруктураТолькоЧтение.Свойство(ТекущееПоле.Имя, ТолькоЧтение) Тогда
		
		ТекущееПоле.ТолькоПросмотр = ТолькоЧтение;
		
	иначе // СтруктураТолькоЧтение.Свойство(ТекущееПоле.Имя, ТолькоЧтение)
		
		ТекущееПоле.ТолькоПросмотр = Ложь;
		
	КонецЕсли; // СтруктураТолькоЧтение.Свойство(ТекущееПоле.Имя, ТолькоЧтение)
	
	
	
	
КонецПроцедуры


&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере(Объект.Проект, АвтосозданиеНедостающихРесурсов, РодительСсылка);
	
		
	РаботыДЗ.ПолучитьЭлементы().Очистить();
	ТаблицаРесурсов.Очистить();

КонецПроцедуры

 

#КонецОбласти


//2.2.3 
// Алгоритм распределения  

//2.3
// Добавлено формирование кода wbs по ИД работы сметы
//Добавлена обработка работ-ресурсов с типом 2 - оборудование
//Добавлена обработка шифра в формате биай (с "-00")
//Добавлено заполнение новых ресурсов - группы ресурсов - КаталогBiGroup
//Добавлено определение ставки СП из первых строк файла для форматов где не указана ставка СП
//Добавлен алгоритм корректировки зарплаты с надбавками и распределение разницы по ресурсам
//Добавлено определение формата файла на основании данных файла
//Добавлено игнорирование строк работ с типом "3" - заемное оборудование? - их не в смете
//Решена проблема с тем что не загружаются материалы с нормой менее 6 знаков, (в файл пишется только норма) - в обработке точность нормы увеличена до 10 знаков

// 2.3.1
// Добавлены алтернативные ветки поиска по наименованию для ресурсов и работ если не указан шифр или указан условный шифр, типа "Т" или  "ПРАЙС"
// добавлена возможность множественного выбора файлов загрузки

//2.3.2
// добавлены правила округления для формата 2018
// добавлен расчет накладных расходов для формата 2018
// оптимизирован поиск нормативных сборников