&НаСервере
Функция ЗагрузитьПроектыНаСервере()
	
	СписокИдентификаторов = Новый СписокЗначений;
	Строки = ТаблицаПроектов.НайтиСтроки(Новый Структура("Отметка", Истина));
	Для Каждого Строка Из Строки Цикл
		СписокИдентификаторов.Добавить(Строка.ПроектГУИД);
	КонецЦикла;
	
	Если СписокИдентификаторов.Количество() = 0 Тогда
		СписокИдентификаторов = Неопределено;
	КонецЕсли;
	
	Возврат РеквизитФормыВЗначение("Объект").ЗагрузитьПроекты(СписокИдентификаторов);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПроекты(Команда)
	СписокУГПР = ЗагрузитьПроектыНаСервере();
	Список.Параметры.УстановитьЗначениеПараметра("СписокУГПР", СписокУГПР);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//{{ BI-Digital: Кашкинбаев Т.А. по заявке 170805 от 21.06.2018 11:00
	Обработки.ИНАГРО_ИнтеграцияPrimavera.ПроверитьСтрокуСоединения();
	//}} BI-Digital: Кашкинбаев Т.А. по заявке 170805 от 21.06.2018 11:00
	
	Список.Параметры.УстановитьЗначениеПараметра("СписокУГПР", Новый СписокЗначений);
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК Отметка,
	|	dbo_Outgoing_Projects.Project_Guid_1C КАК ПроектГУИД,
	|	dbo_Outgoing_Projects.Project_Name КАК Наименование,
	|	dbo_Outgoing_Projects.Project_Short_Name КАК НаименованиеКороткое,
	|	dbo_Outgoing_Projects.ExportDateTime КАК ДатаЭкспорта
	|ИЗ
	|	ВнешнийИсточникДанных.ИНАГРО_ИнтеграционнаяБазаДанных.Таблица.dbo_Outgoing_Projects КАК dbo_Outgoing_Projects";
	
	ТаблицаПроектов.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура АнализРесурсовНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	dbo_Outgoing_Resources.Ссылка,
	|	dbo_Outgoing_Resources.id,
	|	dbo_Outgoing_Resources.Project_GUID_1C,
	|	dbo_Outgoing_Resources.Rsrc_Name,
	|	dbo_Outgoing_Resources.Rsrc_guid_1c,
	|	dbo_Outgoing_Resources.rsrc_type,
	|	dbo_Outgoing_Resources.Rsrc_Short_Name,
	|	dbo_Outgoing_Resources.uom_guid_1c,
	|	dbo_Outgoing_Resources.rsrc_rate,
	|	dbo_Outgoing_Resources.rsrc_contract_rate,
	|	dbo_Outgoing_Resources.Rsrc_Qty,
	|	dbo_Outgoing_Resources.unit_abbrev,
	|	dbo_Outgoing_Resources.unit_name,
	|	dbo_Outgoing_Resources.UserName,
	|	dbo_Outgoing_Resources.res_normative_code,
	|	dbo_Outgoing_Resources.rsrc_est_type,
	|	dbo_Outgoing_Resources.Task_GUID_1C,
	|	dbo_Outgoing_Resources.DateTime,
	|	dbo_Outgoing_Resources.user_name,
	|	dbo_Outgoing_Resources.host_name,
	|	dbo_Outgoing_Resources.pm_rsrc_id,
	|	dbo_Outgoing_Resources.Rsrc_Actual_Qty,
	|	dbo_Outgoing_Resources.act_start_date,
	|	dbo_Outgoing_Resources.act_end_date,
	|	dbo_Outgoing_Resources.target_start_date,
	|	dbo_Outgoing_Resources.target_end_date
	|ИЗ
	|	ВнешнийИсточникДанных.ИНАГРО_ИнтеграционнаяБазаДанных.Таблица.dbo_Outgoing_Resources КАК dbo_Outgoing_Resources
	|ГДЕ
	|	(dbo_Outgoing_Resources.Project_GUID_1C = &ПроектГУИД
	|			ИЛИ &ПроектГУИД = """")";
	
	Запрос.УстановитьПараметр("ПроектГУИД",ПроектГУИД);
	
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ОбработкаОбъект.ПолучитьМакет("Макет");
	ОбластьШапка = Макет.ПолучитьОбласть("АнализРесурсовШапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("АнализРесурсовСтрока");
	ТабДок.Вывести(ОбластьШапка);
	
	
	СпособПоиска = Новый Массив;
	СпособПоиска.Добавить("ПоИдентификатору");
	
	ПараметрыПоиска = Новый Структура();
	
	Реквизиты = Новый Структура();
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ГУИД = Выборка.Rsrc_guid_1c;
		ПараметрыПоиска.Вставить("ГУИД", ГУИД);
		
		Реквизиты.Вставить("Наименование", Выборка.Rsrc_Short_Name);
		Реквизиты.Вставить("ДлинноеНаименование", Выборка.Rsrc_Name);
		
		Отказ = Ложь;
		Ссылка = ОбработкаОбъект.ПолучитьСправочник(Отказ, "СМ_Ресурсы", СпособПоиска, ПараметрыПоиска, Реквизиты);
		
		Если Не Ссылка.Пустая() Тогда
			Если НЕ Ссылка.ЭтоГруппа Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ОбластьСтрока.Параметры.ГУИД = ГУИД;
		ОбластьСтрока.Параметры.Ссылка = Ссылка;
		ТабДок.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	РезультатАнализа.Очистить();
	РезультатАнализа.Вывести(ТабДок);
КонецПроцедуры

&НаКлиенте
Процедура АнализРесурсов(Команда)
	АнализРесурсовНаСервере();
КонецПроцедуры
